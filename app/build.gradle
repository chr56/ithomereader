apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-parcelize'

apply from: 'config.gradle'

def getGitHash = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

def signingConfigFile = rootProject.file("signing.properties")
def signingProperties = new Properties()
signingProperties.load(new FileInputStream(signingConfigFile))

android {
    namespace "me.ikirby.ithomereader"
    compileSdk = 34
    buildToolsVersion = "34.0.0"

    signingConfigs {
        release {
            keyAlias signingProperties['keyAlias']
            keyPassword signingProperties['keyPassword']
            storeFile file(signingProperties['storeFile'])
            storePassword signingProperties['storePassword']
        }
    }

    defaultConfig {
        applicationId "me.ikirby.ithomereader"
        minSdkVersion 23
        targetSdkVersion 34
        versionCode 179
        versionName "5.0.0-beta09"
        resConfigs "zh-rCN"
        versionNameSuffix '-' + getGitHash()
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            applicationIdSuffix ".debug"
        }
        RC {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            applicationIdSuffix ".rc"
            versionNameSuffix "-rc"
        }
    }
    flavorDimensions = List.of("update")
    productFlavors {
        normal {
            dimension 'update'
            manifestPlaceholders = [updateUrl: updateUrl]
        }
        noupdate {
            dimension 'update'
            versionNameSuffix '-pub'
            manifestPlaceholders = [updateUrl: '']
        }
    }
    packagingOptions {
        pickFirst("META-INF/atomicfu.kotlin_module")
        exclude 'META-INF/CHANGES'
        exclude 'META-INF/README.md'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/*.version'
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildFeatures {
        dataBinding = true
        viewBinding = true
        buildConfig = true
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.7.3'
    implementation 'androidx.core:core-ktx:1.12.0'
    implementation 'androidx.viewpager:viewpager:1.0.0'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation 'androidx.fragment:fragment-ktx:1.6.1'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'androidx.preference:preference-ktx:1.2.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.browser:browser:1.6.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'com.google.android.material:material:1.8.0'
    implementation 'org.jsoup:jsoup:1.16.1'
    implementation 'com.github.chrisbanes:PhotoView:2.3.0'
    implementation 'com.github.bumptech.glide:glide:4.12.0'
    kapt 'com.github.bumptech.glide:compiler:4.12.0'

    def moshiVersion = '1.12.0'
    implementation 'com.squareup.okhttp3:okhttp:4.11.0'
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation "com.squareup.moshi:moshi:$moshiVersion"
    implementation "com.squareup.moshi:moshi-kotlin:$moshiVersion"
    implementation "com.squareup.moshi:moshi-adapters:$moshiVersion"
    implementation 'com.squareup.retrofit2:converter-moshi:2.9.0'
    //    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.5.4'
    //    releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.4'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}